plugins {
	id 'net.minecraftforge.gradle' version '6.+'
	id 'idea'
	id "maven-publish"
	id "com.matthewprenger.cursegradle" version "1.4.0"
	id "com.modrinth.minotaur" version "2.8.7"
	id "org.parchmentmc.librarian.forgegradle" version "1.+"
}

group = "com.thedarkcolour.forestry"
version = System.getenv("FORESTRY_VERSION") ?: "1.0.0"
archivesBaseName = "forestry-$minecraft_version"

repositories {
	maven {
		name = "JEI"
		url = "https://dvs1.progwml6.com/files/maven"
		content { includeGroup "mezz.jei" }
	}

	maven {
		name = "Patchouli"
		url = "https://maven.blamejared.com/"
	}
	maven {
		url "https://cursemaven.com"
		content {
			includeGroup "curse.maven"
		}
	}
}

dependencies {
	minecraft "net.minecraftforge:forge:$minecraft_version-$forge_version"

	compileOnly fg.deobf("mezz.jei:jei-$minecraft_version-common-api:$jei_version")
	compileOnly fg.deobf("mezz.jei:jei-$minecraft_version-forge-api:$jei_version")
	runtimeOnly fg.deobf("mezz.jei:jei-$minecraft_version-forge:$jei_version")

	compileOnly fg.deobf("vazkii.patchouli:Patchouli:$patchouli_version:api")
	runtimeOnly fg.deobf("vazkii.patchouli:Patchouli:$patchouli_version")
	runtimeOnly fg.deobf("curse.maven:spit-it-out-857141:4888738")
	runtimeOnly fg.deobf("curse.maven:cyanide-541676:4126944")
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

sourceSets {
	main {
		resources {
			srcDir "src/generated/resources"
		}
	}
}

minecraft {
	mappings channel: 'parchment', version: "$parchment_mappings-$minecraft_version"

	accessTransformer = file("src/main/resources/META-INF/accesstransformer.cfg")

	runs {
		def commonRunProperties = {
			workingDirectory project.file("run")

			property "forge.logging.console.level", "debug"
			property "fml.earlyprogresswindow", "false"

			// See https://github.com/Vazkii/Patchouli#mixin-troubleshooting
			property "mixin.env.remapRefMap", "true"
			property "mixin.env.refMapRemappingFile", "$projectDir/build/createSrgToMcp/output.srg"

			mods {
				forestry {
					source sourceSets.main
				}
			}
		}

		client {
			with commonRunProperties
		}

		server {
			workingDirectory project.file("run/server")
			with commonRunProperties
		}

		data {
			with commonRunProperties
			args "--mod", "forestry", "--all", "--output", file("src/generated/resources/"), "--existing", file("src/main/resources/")
		}
	}
}

def replaceProperties = [
		"version": project.version,
		"forge_version_range": forge_version_range,
		"jei_version_range": jei_version_range,
		"patchouli_version_range": patchouli_version_range
]

processResources {
	inputs.properties replaceProperties
	replaceProperties.put 'project', project

	filesMatching(['META-INF/mods.toml']) {
		expand replaceProperties
	}
}

jar {
	finalizedBy "reobfJar"

	from sourceSets.main.output.classesDirs
	from sourceSets.main.output.resourcesDir

	manifest {
		attributes([
				"Specification-Title"     : "Forestry",
				"Specification-Vendor"    : "SirSengir",
				"Specification-Version"   : "${project.version}",
				"Implementation-Title"    : "${project.name}",
				"Implementation-Version"  : "${project.version}",
				"Implementation-Vendor"   : "SirSengir",
				"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
		])
	}
}

def publicApiIncludePatterns = {
	include "forestry/api/**"
	include "genetics/api/**"
}

javadoc {
	source = sourceSets.main.allJava
	classpath = sourceSets.main.compileClasspath + sourceSets.main.output

	options.addStringOption("Xdoclint:none", "-quiet")
	options.encoding = "UTF-8"
}

javadoc publicApiIncludePatterns

task javadocJar(type: Jar, dependsOn: javadoc, group: "build") {
	archiveClassifier.set("javadoc")
	from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
	archiveClassifier.set("sources")
	from sourceSets.main.allJava
}

task apiJar(type: Jar, group: "build") {
	archiveClassifier.set("api")
	// api jar ist just a development aid and serves as both a binary and source jar simultaneously
	from sourceSets.main.output
	from sourceSets.main.allJava
}

apiJar publicApiIncludePatterns

artifacts {
	archives javadocJar
	archives sourcesJar
	archives apiJar
}

publishing {
	afterEvaluate {
		publications {
			mavenJava(MavenPublication) {
				groupId = project.group
				artifactId = project.archivesBaseName
				version = project.version

				// ForgeGradle will generate wild dependency definitions, see https://github.com/MinecraftForge/ForgeGradle/issues/584
				// Since we don't actually depend on anything, just remove the entire node.
				pom.withXml {
					asNode().remove(asNode().dependencies)
				}

				from components.java
				artifact sourcesJar
				artifact javadocJar
				artifact apiJar
			}
		}
	}

	repositories {
		maven {
			def releasesRepoUrl = uri("$buildDir/repos/releases")
			def snapshotsRepoUrl = uri("$buildDir/repos/snapshots")

			name = "Project"
			url = version.toString().endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl
		}

		maven {
			credentials {
				username System.getenv("MODMAVEN_USER")
				password System.getenv("MODMAVEN_PASSWORD")
			}

			name = "Modmaven"
			url = "https://modmaven.dev/artifactory/local-releases/"
		}
	}
}

////////////////
// CurseForge
if (System.getenv("CURSEFORGE")) {
	curseforge {
		apiKey = System.getenv("CURSEFORGE")

		project {
			id = project.curseforge_project
			changelogType = "markdown"
			changelog = "View changelog at [the release page](https://github.com/ForestryMC/ForestryMC/releases/tag/${version})"

			if (version.contains("alpha")) {
				releaseType = "alpha"
			} else if (version.contains("beta")) {
				releaseType = "beta"
			} else {
				releaseType = "release"
			}

			addGameVersion project.minecraft_version
			addGameVersion "Forge"

			mainArtifact(jar.archiveFile) {
				displayName = "${project.version}"
				relations {
					optionalDependency 'jei'
					requiredDependency 'patchouli'
				}
			}
		}
	}
}

////////////////
// Modrinth
modrinth {
    token.set(System.getenv("MODRINTH"))
    projectId.set(project.modrinth_project)
    changelog.set("View changelog at [the release page](https://github.com/ForestryMC/ForestryMC/releases/tag/${version})")
    versionNumber.set(project.version)

    if (version.contains("alpha")) {
        versionType.set("alpha")
    } else if (version.contains("beta")) {
        versionType.set("beta")
    } else {
        versionType.set("release")
    }

    uploadFile.set(jar)
    gameVersions.add(minecraft_version)
	loaders.add("forge")

    dependencies {
        optional.project("jei")
		required.project("patchouli")
    }
}

tasks.modrinth.onlyIf {
    System.getenv("MODRINTH")
}

idea {
	module {
		downloadSources = true
		downloadJavadoc = true
	}
}
